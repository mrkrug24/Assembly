<!DOCTYPE html>
<html>

<head>
  <meta charset="utf-8">
  <meta name="viewport" content="width=device-width, initial-scale=1.0">
  <title>05_04_2022</title>
  <link rel="stylesheet" href="https://stackedit.io/style.css" />
</head>

<body class="stackedit">
  <div class="stackedit__left">
    <div class="stackedit__toc">
      
    </div>
  </div>
  <div class="stackedit__right">
    <div class="stackedit__html">
      <hr>
<p><em>1.</em> Реализовать функцию, возвращающую n-ое число последовательности Падована</p>
<pre><code>P(0) = P(1) = P(2) = 1
P(n) = P(n-2) + P(n-3)
</code></pre>
<br>
<pre><code>%include "io.inc"

section .text
global CMAIN
CMAIN:
    push ebp                ; стандартный пролог
    mov ebp, esp            ; стандартный пролог

    sub esp, 4

    GET_DEC 4, [esp]

    call P
    
    PRINT_DEC 4, eax
    NEWLINE
    
    add esp, 4              

    mov esp, ebp            ; стандартный эпилог
    pop ebp                 ; стандартный эпилог
    
    xor eax, eax
    ret


P:
    push ebp                ; стандартный пролог
    mov ebp, esp            ; стандартный пролог
    
    sub esp, 8              ; пространство на стеке, выделенное под
                            ; - сохраненный результат вызова P(n-2)
                            ; - аргумент рекурсивных вызовов функций P(n-2), P(n-3) 

    mov eax, dword [ebp+8]  ; eax &lt;- n
    cmp eax, 2
    jg .recur
    mov eax, 1              ; if (n &lt;= 2) { return 1; }
    jmp .end

.recur:
    ; вызов P(n-2)
    sub eax, 2              ; eax &lt;- (n-2)
    mov dword [esp], eax    ; на стек &lt;- (n-2)
    call P                  ; eax &lt;- P(n-2)
    mov dword [ebp-4], eax  ; сохраненный результат вызова P(n-2)
    
    ; вызов P(n-3)
    mov eax, dword [ebp+8]  ; eax &lt;- n
    sub eax, 3              ; на стек &lt;- (n-3)
    mov dword [esp], eax    ; аргумент вызова P(n-3)
    call P
    
    add eax, dword [ebp-4]  ; eax &lt;- P(n-3) + P(n-2)

.end:
    mov esp, ebp            ; стандартный эпилог
    pop ebp                 ; стандартный эпилог
    
    ret
</code></pre>
<hr>
<p><em>2.</em> Реализовать функцию, возвращающую n-ое число последовательности  Фибоначчи</p>
<pre><code>fib(0) = fib(1) = 1
fib(n) = fib(n-1) + fib(n-2)
</code></pre>
<br>
<pre><code>%include "io.inc"

section .text
global CMAIN
CMAIN:
    push ebp                ; стандартный пролог
    mov ebp, esp            ; стандартный пролог

    sub esp, 4

    GET_DEC 4, [esp]    

    call fib
    
    PRINT_DEC 4, eax
    NEWLINE
    
    add esp, 4
    
    mov esp, ebp            ; стандартный эпилог
    pop ebp                 ; стандартный эпилог
         
    xor eax, eax
    ret

    
fib:
    push ebp                ; стандартный пролог
    mov ebp, esp            ; стандартный пролог
    
    sub esp, 8              ; пространство, выделенное под
                            ; - сохраненный результат вызова fib(n-2)
                            ; - аргумент рекурсивных вызовов функций fib(n-1), fib(n-2) 
    
    cmp dword[ebp+8], 2     ; if (n &lt;= 2) { return 1; }
    jl .n_l_2
    
.recur:
    ; вызов fib(n-1)
    mov eax, [ebp+8]        ; eax &lt;- n
    dec eax                 ; eax &lt;- (n-1)
    mov dword[esp], eax     ; на стек &lt;- fib(n-1)
    call fib                ; eax &lt;- fib(n-1)
    mov dword[ebp-4], eax   ; на стек &lt;- сохраненный результат вызова fib(n-1)
    
    ; вызов fib(n-2)
    mov eax, dword[ebp+8]   ; eax &lt;- n
    sub eax, 2              ; eax &lt;- (n-2)
    mov dword[esp], eax     ; на стек &lt;- (n-2)
    call fib                ; eax &lt;- fib(n-2)
    
    add eax, dword[ebp-4]   ; eax &lt;- fib(n-2) + fib(n-1)
    jmp .end

.n_l_2:
    mov eax, 1              ; if (n &lt;= 2) { return 1; }

.end:
    mov esp, ebp            ; стандартный эпилог
    pop ebp                 ; стандартный эпилог
    
    ret
</code></pre>
<hr>
<p><em>3</em>. Реализовать рекурсивную функцию, которая выводит на стандартный поток вывода числа от n до 1 включительно</p>
<pre><code>void reverse_print(int n) {
    if (n &lt; 1) {
        return;
    }

    printf("%d ", n);
    reverse_print(n-1);
}
</code></pre>
<br>
<pre><code>%include "io.inc"

section .bss
    n resd 1

section .text
global CMAIN
CMAIN:
    push ebp                ; стандартный пролог
    mov ebp, esp            ; стандартный пролог

    GET_DEC 4, [n]

    push dword[n]
    call reverse_print
    
    add esp, 4        

    mov esp, ebp            ; стандартный эпилог
    pop ebp                 ; стандартный эпилог
    
    xor eax, eax
    ret
    
    
reverse_print:
    push ebp                ; стандартный пролог
    mov ebp, esp            ; стандартный пролог

    cmp dword[ebp+8], 1     ; if (n &lt; 1) {
    jl .end                 ;     return;
                            ; }

    PRINT_DEC 4, [ebp+8]    ; print n
    PRINT_CHAR ` `

    ; вызвать reverse_print(n-1)      
    mov eax, [ebp+8]        ; eax &lt;- n
    dec eax                 ; eax &lt;- (n-1)
    push eax                ; на стек &lt;- (n-1)
    call reverse_print
    
    add esp, 4

.end:
    mov esp, ebp            ; стандартный эпилог
    pop ebp                 ; стандартный эпилог

    ret
</code></pre>
<hr>
<p><em>4</em>. Реализовать рекурсивную функцию, возвращающую наибольший общий делитель двух чисел посредством алгоритма Евклида</p>
<pre><code>int gcd(int a, int b) {
    return b ? gcd(b, a % b) : a;
}
</code></pre>
<br>
<pre><code>%include "io.inc"

section .bss
    a resd 1
    b resd 1

section .text
global CMAIN
CMAIN:
    push ebp                ; стандартный пролог
    mov ebp, esp            ; стандартный пролог

    GET_DEC 4, [a]
    GET_DEC 4, [b]    

    push dword[b]
    push dword[a]

    call gcd
    
    PRINT_STRING `gcd(`
    PRINT_DEC 4, [a]
    PRINT_STRING `,`
    PRINT_DEC 4, [b]
    PRINT_STRING `) = `
    PRINT_DEC 4, eax

    add esp, 8

    mov esp, ebp            ; стандартный эпилог
    pop ebp                 ; стандартный эпилог

    xor eax, eax
    ret
    
gcd:
    push ebp                ; стандартный пролог
    mov ebp, esp            ; стандартный пролог
    
    cmp dword[ebp+12], 0    ; if (b == 0) { return a; }
    je .b_zero

    ; отладочный вывод
    PRINT_STRING `a = `
    PRINT_DEC 4, [ebp+8]
    NEWLINE
    PRINT_STRING `b = `
    PRINT_DEC 4, [ebp+12]
    PRINT_STRING `\n\n`
    
    ; 
    
    mov eax, [ebp + 8]  ; eax &lt;- a
    xor edx, edx    
    div DWORD[ebp+12]   ; edx &lt;- a % b

    ; вызов gcd(b, a % b)
    
    push edx            ; на стек &lt;- a % b
    push dword[ebp+12]  ; на стек &lt;- b
                        ; (в обраном порядке)
    call gcd
    
    add esp, 8
    
    jmp .end

.b_zero:
    mov eax, [ebp+8]        ; if (b == 0) { return a; }

.end:
    mov esp, ebp            ; стандартный эпилог
    pop ebp                 ; стандартный эпилог
    
    ret
</code></pre>
<hr>

    </div>
  </div>
</body>

</html>